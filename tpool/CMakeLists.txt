ADD_LIBRARY(tpool STATIC
  aio_simulated.cc
  tpool_structs.h
  CMakeLists.txt
  tpool.h
  tpool_generic.cc
  task_group.cc
  task.cc
  wait_notification.cc
)

TARGET_INCLUDE_DIRECTORIES(tpool PUBLIC "${CMAKE_CURRENT_SOURCE_DIR}"
 PRIVATE ${PROJECT_SOURCE_DIR}/include)

IF(WIN32)
  TARGET_SOURCES(tpool PRIVATE tpool_win.cc aio_win.cc)
ELSEIF(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  TARGET_SOURCES(tpool PRIVATE aio_linux.cc)
  OPTION(WITH_URING "Require that io_uring be used" OFF)
  OPTION(WITH_LIBAIO "Require that libaio is used" OFF)
  IF(WITH_URING)
    SET(URING_REQUIRED REQUIRED)
  ENDIF()
  IF(WITH_LIBAIO)
    SET(LIBAIO_REQUIRED REQUIRED)
  ENDIF()
  FIND_PACKAGE(URING QUIET ${URING_REQUIRED})
  IF(URING_FOUND)
    SET(URING_FOUND ${URING_FOUND} PARENT_SCOPE)
    TARGET_COMPILE_DEFINITIONS(tpool PUBLIC "-DHAVE_URING")
    TARGET_LINK_LIBRARIES(tpool PRIVATE ${URING_LIBRARIES})
    TARGET_INCLUDE_DIRECTORIES(tpool PUBLIC ${URING_INCLUDE_DIRS})
    TARGET_SOURCES(tpool PRIVATE aio_liburing.cc)
    SET(CMAKE_REQUIRED_INCLUDES_SAVE ${CMAKE_REQUIRED_INCLUDES})
    SET(CMAKE_REQUIRED_LIBRARIES_SAVE ${CMAKE_REQUIRED_LIBRARIES})
    SET(CMAKE_REQUIRED_INCLUDES ${URING_INCLUDE_DIRS})
    SET(CMAKE_REQUIRED_LIBRARIES ${URING_LIBRARIES})
    CHECK_SYMBOL_EXISTS(io_uring_mlock_size "liburing.h" HAVE_IO_URING_MLOCK_SIZE)
    SET(CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES_SAVE})
    SET(CMAKE_REQUIRED_LIBRARIES ${CMAKE_REQUIRED_LIBRARIES_SAVE})
    IF(HAVE_IO_URING_MLOCK_SIZE)
      SET_SOURCE_FILES_PROPERTIES(aio_liburing.cc PROPERTIES COMPILE_FLAGS "-DHAVE_IO_URING_MLOCK_SIZE")
    ENDIF()
  ENDIF()

  FIND_PACKAGE(LIBAIO QUIET ${LIBAIO_REQUIRED})
  IF(LIBAIO_FOUND)
    TARGET_COMPILE_DEFINITIONS(tpool PUBLIC  "-DHAVE_LIBAIO")
    TARGET_INCLUDE_DIRECTORIES(tpool PUBLIC ${LIBAIO_INCLUDE_DIRS})
    TARGET_LINK_LIBRARIES(tpool PRIVATE ${LIBAIO_LIBRARIES})
    TARGET_SOURCES(tpool PRIVATE aio_libaio.cc)
  ENDIF()
ENDIF()

IF(URING_FOUND)
  ADD_DEPENDENCIES(tpool GenError)
ENDIF()
